import static org.gradle.api.JavaVersion.VERSION_11

plugins {
    id 'java'
    id 'base'
    id 'jacoco'
    id "com.diffplug.spotless" version "5.12.5"
    id 'maven-publish'
}

allprojects {
    group 'com.github.terziele.swarz'
    version '0.2.0-SNAPSHOT'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configure(subprojects) {
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'maven-publish'

    sourceCompatibility = VERSION_11
    targetCompatibility = VERSION_11

    java {
        withSourcesJar()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    repositories {
        mavenCentral()
    }


    publishing {
        publications {
            maven(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version

                from components.java
            }
        }
    }


    spotless {
        java {
            googleJavaFormat()
            licenseHeader '/* (C)$YEAR */' // or licenseHeaderFile
        }
        groovyGradle {
            greclipse()
            target '*.gradle' // default target for kotlinGradle
        }
    }
}
