import static org.gradle.api.JavaVersion.VERSION_11

plugins {
    id 'java'
    id 'base'
    id 'jacoco'
    id "com.diffplug.spotless" version "5.12.5"
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

allprojects {
    group 'io.github.terziele.swarz'
    version '0.2.0-SNAPSHOT'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configure(subprojects) {
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'maven-publish'

    sourceCompatibility = VERSION_11
    targetCompatibility = VERSION_11

    java {
        withSourcesJar()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    repositories {
        mavenCentral()
    }

    spotless {
        java {
            googleJavaFormat()
            licenseHeader '/* (C)$YEAR */' // or licenseHeaderFile
        }
        groovyGradle {
            greclipse()
            target '*.gradle' // default target for kotlinGradle
        }
    }
}
nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")

        }
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            from components.java
            pom {
                name = 'Swarz'
                description = 'Swarz. Static OpenAPI generator'
                url = 'https://github.com/terziele/swarz'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/terziele/swarz/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'terziele'
                        name = 'Ihnat Aŭtuška'
                        email = 'autushka.ihnat@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/terziele/swarz.git'
                    developerConnection = 'scm:git:ssh://github.com/terziele/swarz.git'
                    url = 'https://github.com/terziele/swarz'
                }
            }
        }
    }

}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
