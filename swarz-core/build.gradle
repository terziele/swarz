plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

repositories {
	mavenCentral()
}

java {
	withJavadocJar()
	withSourcesJar()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = project.group
			artifactId = project.name
			version = project.version
			from components.java
			pom {
				name = project.name
				description = 'Swarz. Static OpenAPI generator. ' + project.name
				url = 'https://github.com/terziele/swarz'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://github.com/terziele/swarz/blob/master/LICENSE'
					}
				}
				developers {
					developer {
						id = 'terziele'
						name = 'Ihnat Aŭtuška'
						email = 'autushka.ihnat@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/terziele/swarz.git'
					developerConnection = 'scm:git:ssh://github.com/terziele/swarz.git'
					url = 'https://github.com/terziele/swarz'
				}
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}

dependencies {
	annotationProcessor "org.projectlombok:lombok:1.18.20"
	compileOnly "org.projectlombok:lombok:1.18.20"

	implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.11.3"
	implementation("javax.servlet:javax.servlet-api:3.1.0")
	implementation("org.mockito:mockito-core:2.23.0")
	implementation("com.google.guava:guava:30.1-jre")
	api("org.springframework.boot:spring-boot-starter:2.5.4")
	api("org.springframework.boot:spring-boot-starter-test:2.5.4")
	api("org.springframework.boot:spring-boot-starter-web:2.5.4")
	api("org.springdoc:springdoc-openapi-ui:1.5.9") {
		exclude(group: "org.springframework", module: "spring-web")
		exclude(group: "org.springframework", module: "spring-webmvc")
	}
	api("org.springdoc:springdoc-openapi-webmvc-core:1.5.9") {
		exclude(group: "org.springframework", module: "spring-web")
		exclude(group: "org.springframework", module: "spring-webmvc")
	}

	testImplementation("org.junit.jupiter:junit-jupiter:5.7.1")
	testImplementation("org.mockito:mockito-core:2.23.0")
	testImplementation("org.mockito:mockito-junit-jupiter:2.23.0")

	testAnnotationProcessor "org.projectlombok:lombok:1.18.20"
	testCompileOnly "org.projectlombok:lombok:1.18.20"
}
